TITLE:
  6.2 CPA 3-Factor Longitudinal ESEM WLSMV – Target Rotation - Strong Invariance Model

DATA:
  FILE = data_ora_cpa.csv;

VARIABLE:

!-------------------------------------------------
! 1. VARIABLES
!-------------------------------------------------
NAMES = ID site gender age_t1 age_t2 cpa_1_t1 cpa_2_t1 cpa_3_rs_t1
  cpa_4_t1 cpa_5_rs_t1 cpa_6_t1 cpa_7_t1 cpa_8_t1 cpa_9_t1 cpa_10_t1
  cpa_11_rs_t1 cpa_12_t1 cpa_1_v2_t2 cpa_2_t2 cpa_3_rs_t2 cpa_4_t2
  cpa_5_v2_t2 cpa_6_t2 cpa_7_t2 cpa_8_t2 cpa_9_t2 cpa_10_t2 cpa_11_rs_t2
  cpa_12_t2;
 
USEVARIABLES = cpa_2_t1 cpa_3_rs_t1
  cpa_4_t1 cpa_6_t1 cpa_7_t1 cpa_8_t1 cpa_9_t1 cpa_10_t1
  cpa_11_rs_t1 cpa_12_t1 cpa_2_t2 cpa_3_rs_t2 cpa_4_t2
 cpa_6_t2 cpa_7_t2 cpa_8_t2 cpa_9_t2 cpa_10_t2 cpa_11_rs_t2
  cpa_12_t2;

  CATEGORICAL =
 cpa_2_t1 cpa_3_rs_t1
  cpa_4_t1  cpa_6_t1 cpa_7_t1 cpa_8_t1 cpa_9_t1 cpa_10_t1
  cpa_11_rs_t1 cpa_12_t1  cpa_2_t2 cpa_3_rs_t2 cpa_4_t2
   cpa_6_t2 cpa_7_t2 cpa_8_t2 cpa_9_t2 cpa_10_t2 cpa_11_rs_t2
  cpa_12_t2;

  MISSING     = ALL (-999);
  IDVARIABLE  = ID;

DEFINE:

CUT  cpa_2_t1 cpa_3_rs_t1
  cpa_4_t1  cpa_6_t1 cpa_7_t1 cpa_8_t1 cpa_9_t1 cpa_10_t1
  cpa_11_rs_t1 cpa_12_t1  cpa_2_t2 cpa_3_rs_t2 cpa_4_t2
   cpa_6_t2 cpa_7_t2 cpa_8_t2 cpa_9_t2 cpa_10_t2 cpa_11_rs_t2
  cpa_12_t2 (2 3 4);

!-------------------------------------------------
! 2. ANALYSIS
!-------------------------------------------------
ANALYSIS:
  TYPE             = GENERAL;
  ESTIMATOR        = WLSMV;
  PARAMETERIZATION = THETA;
  ROTATION         = TARGET;
  ITERATIONS = 10000;

!-------------------------------------------------
! 3. MODEL  (Target matrix inside BY lines)
!-------------------------------------------------
MODEL:

!---------------- WAVE-1  (Factors F1, F2) ----------------
F1_T1 BY
cpa_2_t1~0 cpa_3_rs_t1~0 cpa_4_t1~0 cpa_6_t1~0 cpa_7_t1*
cpa_8_t1~0 cpa_9_t1* cpa_10_t1~0 cpa_11_rs_t1* cpa_12_t1~0 (*t1 L1);

F2_T1 BY
 cpa_2_t1* cpa_3_rs_t1~0 cpa_4_t1*  cpa_6_t1~0
cpa_7_t1~0 cpa_8_t1*  cpa_9_t1~0 cpa_10_t1~0 
cpa_11_rs_t1~0 cpa_12_t1*(*t1 L1);

F3_T1 BY  cpa_2_t1~0 cpa_3_rs_t1* cpa_4_t1~0  cpa_6_t1*
cpa_7_t1~0 cpa_8_t1~0  cpa_9_t1~0 cpa_10_t1* 
cpa_11_rs_t1~0 cpa_12_t1~0(*t1 L1);

!MF_T1 BY cpa_3_rs_t1* cpa_11_rs_t1;

!---------------- WAVE-2  (Factors F3, F4) ----------------
F1_T2 BY
 cpa_2_t2~0 cpa_3_rs_t2~0 cpa_4_t2~0  cpa_6_t2~0 cpa_7_t2*
cpa_8_t2~0 cpa_9_t2* cpa_10_t2~0 cpa_11_rs_t2* cpa_12_t2~0 (*t2 L1);

F2_T2 BY
 cpa_2_t2* cpa_3_rs_t2~0 cpa_4_t2* cpa_6_t2~0 cpa_7_t2~0
cpa_8_t2* cpa_9_t2~0 cpa_10_t2~0  cpa_11_rs_t2~0 cpa_12_t2*(*t2 L1);

F3_T2 BY  cpa_2_t2~0 cpa_3_rs_t2* cpa_4_t2~0
 cpa_6_t2* cpa_7_t2~0 cpa_8_t2~0 cpa_9_t2~0 cpa_10_t2* cpa_11_rs_t2~0
  cpa_12_t2~0 (*t2 L1);

!MF_T2 BY cpa_3_rs_t2* cpa_11_rs_t2;

! same-item residual correlations across waves
cpa_2_t1   WITH cpa_2_t2 ;
cpa_3_rs_t1 WITH cpa_3_rs_t2;
cpa_4_t1   WITH cpa_4_t2 ;
cpa_6_t1   WITH cpa_6_t2 ;
cpa_7_t1   WITH cpa_7_t2 ;
cpa_8_t1   WITH cpa_8_t2 ;
cpa_9_t1   WITH cpa_9_t2 ;
cpa_10_t1  WITH cpa_10_t2 ;
cpa_11_rs_t1 WITH cpa_11_rs_t2 ;
cpa_12_t1  WITH cpa_12_t2 ;

!factor means
[F1_T1@0];
[F2_T1@0];
[F3_T1@0];
[F1_T2*];
[F2_T2*];
[F3_T2*];
![MF_T1@0];
![MF_T2@0];

  !factor variances
  F1_T1@1;
  F2_T1@1;
  F3_T1@1;
  F1_T2*;
  F2_T2*;
  F3_T2*;
! MF_T1@1;
! MF_T2@1;


!uniquenesses
cpa_2_t1-cpa_12_t1@1;
cpa_2_t2-cpa_12_t2;
cpa_3_rs_t1 WITH cpa_11_rs_t1;
cpa_3_rs_t2 WITH cpa_11_rs_t2;

  !factor correlations
!  MF_T1 WITH F1_T1@0 F2_T1@0 F3_T1@0 F1_T2@0 F2_T2@0 F3_T2@0;
!  MF_T2 WITH F1_T1@0 F2_T1@0 F3_T1@0 F1_T2@0 F2_T2@0 F3_T2@0;

  !---- CPA-2  ------------------------------------------
  [cpa_2_t1$1] (t2);        
  [cpa_2_t2$1] (t2);
  [cpa_2_t1$2] (t2_2);   
  [cpa_2_t2$2] (t2_2);
  [cpa_2_t1$3] (t2_3);  
   [cpa_2_t2$3] (t2_3);
  !---- CPA-3 ------------------------------------------
  [cpa_3_rs_t1$1] (t3);        
  [cpa_3_rs_t2$1] (t3);
  [cpa_3_rs_t1$2] (t3_2);   
  [cpa_3_rs_t2$2] (t3_2);
  [cpa_3_rs_t1$3] (t3_3);   
  [cpa_3_rs_t2$3] (t3_3);

  !---- CPA-4 ------------------------------------------
  [cpa_4_t1$1] (t4);        
  [cpa_4_t2$1] (t4);
  [cpa_4_t1$2] (t4_2);   
  [cpa_4_t2$2] (t4_2);
  [cpa_4_t1$3] (t4_3);  
  [cpa_4_t2$3] (t4_3);

  !---- CPA-6 ------------------------------------------
  [cpa_6_t1$1] (t6);        
  [cpa_6_t2$1] (t6);
  [cpa_6_t1$2] (t6_2);   
  [cpa_6_t2$2] (t6_2);
  [cpa_6_t1$3] (t6_3);   
  [cpa_6_t2$3] (t6_3);

  !---- CPA-7 ------------------------------------------
  [cpa_7_t1$1] (t7);        
  [cpa_7_t2$1] (t7);
  [cpa_7_t1$2] (t7_2);   
  [cpa_7_t2$2] (t7_2);
  [cpa_7_t1$3] (t7_3);   
  [cpa_7_t2$3] (t7_3);

  !---- CPA-8 ------------------------------------------
  [cpa_8_t1$1] (t8);        
  [cpa_8_t2$1] (t8);
  [cpa_8_t1$2] (t8_2);   
  [cpa_8_t2$2] (t8_2);
  [cpa_8_t1$3] (t8_3);   
  [cpa_8_t2$3] (t8_3);
  !---- CPA-9 ------------------------------------------
  [cpa_9_t1$1] (t9);       
   [cpa_9_t2$1] (t9);
  [cpa_9_t1$2] (t9_2);   
  [cpa_9_t2$2] (t9_2);
  [cpa_9_t1$3] (t9_3);   
  [cpa_9_t2$3] (t9_3);

  !---- CPA-10 -----------------------------------------
  [cpa_10_t1$1] (t10);       
  [cpa_10_t2$1] (t10);
  [cpa_10_t1$2] (t10_2);  
  [cpa_10_t2$2] (t10_2);
  [cpa_10_t1$3] (t10_3);  
  [cpa_10_t2$3] (t10_3);

  !---- CPA-11 (rev-scored) -----------------------------
  [cpa_11_rs_t1$1] (t11);   
  [cpa_11_rs_t2$1] (t11);
  [cpa_11_rs_t1$2] (t11_2); 
  [cpa_11_rs_t2$2] (t11_2);
  [cpa_11_rs_t1$3] (t11_3); 
  [cpa_11_rs_t2$3] (t11_3);

  !---- CPA-12 -----------------------------------------
  [cpa_12_t1$1] (t12);      
  [cpa_12_t2$1] (t12);
  [cpa_12_t1$2] (t12_2);  
  [cpa_12_t2$2] (t12_2);
  [cpa_12_t1$3] (t12_3);  
  [cpa_12_t2$3] (t12_3);

!-------------------------------------------------
OUTPUT:
  SAMPSTAT STANDARDIZED MODINDICES (ALL)
  CINTERVAL RESIDUAL SVALUES TECH1 TECH3 TECH4;
