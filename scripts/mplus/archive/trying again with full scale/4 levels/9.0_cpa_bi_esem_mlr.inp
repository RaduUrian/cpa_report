TITLE:
  9.0 CPA 2-Factor Longitudinal bi-factor ESEM – MLR

DATA:
  FILE = data_ora_cpa.csv;

!-------------------------------------------------
! 1. VARIABLES  (drop items 1 & 5)
!-------------------------------------------------
VARIABLE:
  NAMES =
      ID site gender age_t1 age_t2
      cpa_2_t1 cpa_4_t1 cpa_6_t1 cpa_7_t1 cpa_8_t1 cpa_9_t1
      cpa_10_t1 cpa_11_rs_t1 cpa_12_t1
      cpa_2_t2 cpa_4_t2 cpa_6_t2 cpa_7_t2 cpa_8_t2 cpa_9_t2
      cpa_10_t2 cpa_11_rs_t2 cpa_12_t2 ;

  USEVARIABLES =
      cpa_2_t1 cpa_4_t1 cpa_6_t1 cpa_7_t1 cpa_8_t1 cpa_9_t1
      cpa_10_t1 cpa_11_rs_t1 cpa_12_t1
      cpa_2_t2 cpa_4_t2 cpa_6_t2 cpa_7_t2 cpa_8_t2 cpa_9_t2
      cpa_10_t2 cpa_11_rs_t2 cpa_12_t2 ;

  MISSING     = ALL (-999);
  IDVARIABLE  = ID;

!-------------------------------------------------
! 2. ANALYSIS
!-------------------------------------------------
ANALYSIS:
  TYPE             = GENERAL;
  ESTIMATOR        = MLR;
  ROTATION         = TARGET (orthogonal);
  ITERATIONS = 10000;

!-------------------------------------------------
! 3. MODEL  (Target matrix inside BY lines)
!-------------------------------------------------
MODEL:

!---------------- WAVE-1  (Factors F1, F2) ----------------
G_T1 BY cpa_2_t1*    cpa_6_t1    cpa_7_t1    cpa_9_t1
cpa_10_t1   cpa_11_rs_t1
cpa_4_t1   cpa_8_t1   cpa_12_t1 (*t1);

F1_T1 BY
cpa_2_t1~0    cpa_6_t1~0    cpa_7_t1*    cpa_9_t1*
cpa_10_t1~0   cpa_11_rs_t1*
cpa_4_t1~0   cpa_8_t1~0   cpa_12_t1~0 (*t1);

F2_T1 BY
cpa_4_t1*    cpa_8_t1*    cpa_12_t1*
cpa_2_t1*   cpa_6_t1*   cpa_7_t1~0   cpa_9_t1~0
cpa_10_t1*  cpa_11_rs_t1~0 (*t1);

!---------------- WAVE-2  (Factors F3, F4) ----------------
G_T2 BY cpa_4_t2*    cpa_8_t2    cpa_12_t2
cpa_2_t2   cpa_6_t2   cpa_7_t2   cpa_9_t2
cpa_10_t2  cpa_11_rs_t2 (*t2);

F1_T2 BY
cpa_2_t2~0    cpa_6_t2~0    cpa_7_t2*    cpa_9_t2*
cpa_10_t2~0   cpa_11_rs_t2*
cpa_4_t2~0   cpa_8_t2~0   cpa_12_t2~0 (*t2);

F2_T2 BY
cpa_4_t2*    cpa_8_t2*    cpa_12_t2*
cpa_2_t2*   cpa_6_t2*   cpa_7_t2~0   cpa_9_t2~0
cpa_10_t2*  cpa_11_rs_t2~0 (*t2);

! same-item residual correlations across waves
cpa_2_t1   WITH cpa_2_t2 ;
cpa_4_t1   WITH cpa_4_t2 ;
cpa_6_t1   WITH cpa_6_t2 ;
cpa_7_t1   WITH cpa_7_t2 ;
cpa_8_t1   WITH cpa_8_t2 ;
cpa_9_t1   WITH cpa_9_t2 ;
cpa_10_t1  WITH cpa_10_t2 ;
cpa_11_rs_t1 WITH cpa_11_rs_t2 ;
cpa_12_t1  WITH cpa_12_t2 ;

!intercepts free
[cpa_2_t1-cpa_12_t2];

!factor means fixed to 0
[F1_T1@0];
[F2_T1@0];
[F1_T2@0];
[F2_T2@0];
[G_T1@0];
[G_T2@0];

!factor variances fixed to 1
F1_T1@1;
F2_T1@1;
F1_T2@1;
F2_T2@1;
G_T1@1;
G_T2@1;

!uniquenesses free
cpa_2_t1-cpa_12_t1;
cpa_2_t2-cpa_12_t2;

!factor correlations
!G_T1 WITH F1_T1@0 F2_T1@0 F1_T2@0 F2_T2@0;
!G_T2 WITH F1_T1@0 F2_T1@0 F1_T2@0 F2_T2@0;

!-------------------------------------------------
OUTPUT:
  SAMPSTAT STANDARDIZED MODINDICES (ALL)
  CINTERVAL RESIDUAL SVALUES TECH1 TECH3 TECH4;
